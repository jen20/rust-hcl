object = { single_object | nested_object }

single_object = _{ ident ~ "{" ~ assignment* ~ "}" }

nested_object = _{ ident+ ~ "\"" ~ ident ~ "\"" ~ "{" ~ assignment* ~ "}" }

ident =  @{
    ('a'..'z' | 'A'..'Z') ~ ident_char* |
    "_" ~ ident_char+
}
ident_char = _{ 'a'..'z' | 'A'..'Z' | '0'..'9' | "_" }

assignment = { ident ~ "=" ~ value }

value = _{ list | object | string | number | boolean }

string = @{ double_quote ~ (!double_quote ~ any)* ~ double_quote }

double_quote = _{ "\"" }

number = { float | int }
float = ${
  int ~ "." ~ int? ~ exp? |
  int ~ exp
}
int = { digit+ }
digit = _{ '0'..'9' }
exp = ${ ^"e" ~ (plus | minus)? ~ int }
plus = { "+" }
minus = { "-" }

boolean = { true_lit | false_lit }
true_lit = { "true" }
false_lit = { "false" }

list = { "[" ~ list_item* ~ last_list_item? ~ "]" }
list_item = _{ value ~ comma }
comma = _{ "," }
last_list_item = _{ value ~ comma? }

whitespace = _{ " " | "\t" | newline }
newline = _{ "\r\n" | "\n" }

comment = { inline_comment | block_comment }
inline_comment = _{ (pound | double_slash) ~ (!eo_inline_comment ~ any)* }
pound = _{ "#" }
double_slash = _{ "//" }
eo_inline_comment = _{ newline | eoi }
block_comment = _{ "/*" ~ (!"*/" ~ any)* ~ "*/" }
