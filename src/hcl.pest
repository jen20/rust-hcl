// The top-level scope of an HCL file
hcl = { soi ~ (nested_object | assignment | pair )* ~ eoi }

// Key-value pairs, possibly nested
nested_object = { keys ~ object }
assignment = { key ~ equals ~ value ~ comma? }
pair = { key ~ colon ~ value ~ comma? }

// Object keys
keys = { key* }
key = _{ ident | string }

// Identifiers
ident = @{ ident_first_char ~ ident_char* }
ident_char = _{ letter | digit | dash | dot }
ident_first_char = _{ letter | underscore }
letter = _{ 'a'..'z' | 'A'..'Z' }

// Values
value = _{ list | object | string | number | boolean }

// Lists
list = { lbracket ~ list_item* ~ last_list_item? ~ rbracket }
list_item = _{ value ~ comma }
last_list_item = _{ value ~ comma? }

// Object literals
object = { lbrace ~ (assignment | pair)* ~ rbrace }

// Strings
string = @{ double_quote ~ (!double_quote ~ any)* ~ double_quote }

// Numbers
number = _{ float | int }
float = ${ "-"? ~ int ~ dot ~ int? ~ exp? | int ~ exp }
int = { "-"? ~ digit+ }
exp = ${ ^"e" ~ (plus | minus)? ~ int }
digit = _{ '0'..'9' }

// Booleans
boolean = { true_lit | false_lit }
true_lit = { "true" }
false_lit = { "false" }

// Punctuation
colon = _{ ":" }
comma = _{ "," }
dash = _{ "-" }
dot = _{ "." }
double_quote = _{ "\"" }
equals = _{ "=" }
lbrace = _{ "{" }
lbracket = _{ "[" }
minus = { "-" }
plus = { "+" }
pound = _{ "#" }
rbrace = _{ "}" }
rbracket = _{ "]" }
underscore = _{ "_" }

// Comments
comment = { inline_comment | block_comment }
inline_comment = _{ (pound | double_slash) ~ (!eo_inline_comment ~ any)* }
block_comment = _{ "/*" ~ (!"*/" ~ any)* ~ "*/" }
double_slash = _{ "//" }
eo_inline_comment = _{ newline | eoi }

// Whitespace
whitespace = _{ space | tab | newline }
space = _{ " " }
tab = _{ "\t" }
newline = _{ windows_newline | unix_newline }
windows_newline = _{ "\r\n" }
unix_newline = _{ "\n" }
